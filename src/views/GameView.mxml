<?xml version="1.0" encoding="utf-8"?>
<views:AbstractRefView xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark" title="GameView"  
		xmlns:components="components.*" xmlns:views="views.*">	
	<fx:Script>
		<![CDATA[
			import components.CheckGameEndPopUp;
			
			import events.AddTeamEvent;
			import events.EditTeamEvent;
			import events.EndGamePopUpButtonEvent;
			import events.GameCompleteEvent;
			import events.GameTimeExpiredEvent;
			import events.TeamSelectedEvent;
			
			import mx.managers.PopUpManager;
			
			import spark.events.ViewNavigatorEvent;

			[Bindable]
			protected var halfInning:String = RefConstants.INNING_TOP;	
			private static const BALL		: String = "ball";
			private static const STRIKE		: String = "strike";
			private static const FOUL		: String = "foul";
			private static const OUT		: String = "out";
			private static const HOMESCORE	: String = "home";
			private static const AWAYSCORE	: String = "away";
			
			private var _endPop : CheckGameEndPopUp;
			
			override protected function viewActivateHandler(event:ViewNavigatorEvent):void{
				super.viewActivateHandler(event);
				headRefModel.gameModel.dispatcher.addEventListener( GameCompleteEvent.GAME_COMPLETE, handleGameComplete );
			}
			
			protected function undoButtonClickHandler(event:MouseEvent):void{
				switch(headRefModel.undoStack.pop()){
					case BALL: 		headRefModel.gameModel.balls--; 		break;
					case STRIKE:	headRefModel.gameModel.strikes--; 		break;
					case FOUL: 		headRefModel.gameModel.fouls--;			break;
					case OUT: 		headRefModel.gameModel.outs--;			break;
					case HOMESCORE:	headRefModel.gameModel.homeTeamScore--;	break;
					case AWAYSCORE:	headRefModel.gameModel.awayTeamScore--;	break;
					default:break;
				}
			}

			protected function foulBtnClickHandler(event:MouseEvent):void{
				headRefModel.gameModel.fouls++;
				headRefModel.undoStack.push(FOUL);
			}
			
			protected function strikeBtnClickHandler(event:MouseEvent):void{
				headRefModel.gameModel.strikes++;
				headRefModel.undoStack.push(STRIKE);
			}
			
			protected function ballBtnClickHandler(event:MouseEvent):void{				
				headRefModel.gameModel.balls++;
				headRefModel.undoStack.push(BALL);
			}
			
			protected function awayBtnClickHandler():void{
				headRefModel.gameModel.awayTeamScore++;
				headRefModel.undoStack.push(AWAYSCORE);
			}
			
			protected function homeBtnClickHandler():void{				
				headRefModel.gameModel.homeTeamScore++;
				headRefModel.undoStack.push(HOMESCORE);
			}
			
			protected function endButtonClickHandler(event:MouseEvent):void{
				headRefModel.gameModel.endGame();
			}
			
			protected function handleGameComplete(event:GameCompleteEvent):void{
				deployPopUp();			
			}
			
			protected function continueGame(evt:EndGamePopUpButtonEvent):void{
				PopUpManager.removePopUp(_endPop);
			}
			
			protected function goToEndView(evt:EndGamePopUpButtonEvent):void{
				gameTimer.dispose();
				PopUpManager.removePopUp(_endPop);
				goToView(EndView);
			}
			
			protected function safeBtnClickHandler(event:MouseEvent):void{
				headRefModel.gameModel.kickerSafe();
			}
			
			protected function outBtnClickHandler(event:MouseEvent):void{
				headRefModel.gameModel.kickerOut();
				halfInning = headRefModel.gameModel.halfInning;
				headRefModel.undoStack.push(OUT);
			}			
			
			protected function addTeamHandler(event:AddTeamEvent):void{
				headRefModel.homeOrAwayAdded = (event.currentTarget.id == homeBtn.id)?RefConstants.HOME:RefConstants.AWAY;
				goToView(EditTeamView);
			}
			
			
			protected function editTeamHandler(event:EditTeamEvent):void{
				headRefModel.teamSelectedForEditing = event.teamToEdit;
				goToView(EditTeamView);
				
			}
			
			protected function gameTimeExpiredHandler(event:GameTimeExpiredEvent):void{
				deployPopUp();
				_endPop.optionalMessage = "Time's Up!";
			}
			
			
			private function deployPopUp():void{
				_endPop = new CheckGameEndPopUp();
				_endPop.setStyle("modalTransparency", .75);
				_endPop.setStyle("modalTransparencyBlur", .75);
				_endPop.addEventListener(EndGamePopUpButtonEvent.END_CLICK,goToEndView);
				_endPop.addEventListener(EndGamePopUpButtonEvent.CONTINUE_CLICK,continueGame);
				PopUpManager.addPopUp(_endPop,this,true);
				PopUpManager.centerPopUp(_endPop);		
			}
			
			protected function homeTeamSelectedHandler(event:TeamSelectedEvent):void{
				headRefModel.gameModel.homeTeam = event.teamSelected;
				
			}
			
			protected function awayTeamSelectedHandler(event:TeamSelectedEvent):void{
				headRefModel.gameModel.awayTeam = event.teamSelected;
			}

			
		]]>
	</fx:Script>

	<s:Label  x="5"	 	y="5" 	text="{headRefModel.gameModel.leagueRules.leagueName}" styleName="ruleTypeLabel"/>
	<s:Button right="5"	y="5"	mouseDown="endButtonClickHandler(event)" skinClass="skinClasses.EndGameBtnSkin"/>

	<components:TeamButton 	y="25"
							id="homeBtn" 
							homeOrAway="{TeamButton.HOME}"
							teamName="{headRefModel.gameModel.homeTeam.teamName}" 
						   	score="{headRefModel.gameModel.homeTeamScore}"  
							scoreClick="homeBtnClickHandler()" 
							teamColor="{headRefModel.gameModel.homeTeam.teamColor}"
							dataProvider="{headRefModel.teams}"
							addTeam="addTeamHandler(event)" 
							editTeam="editTeamHandler(event)"
							teamSelected="homeTeamSelectedHandler(event)" />
	
	<components:TeamButton 	y="25" right="0"
							id="awayBtn" 
							homeOrAway="{TeamButton.AWAY}"
						   	teamName="{headRefModel.gameModel.awayTeam.teamName}" 
							score="{headRefModel.gameModel.awayTeamScore}" 
			  				scoreClick="awayBtnClickHandler()"
							teamColor="{headRefModel.gameModel.awayTeam.teamColor}"
							dataProvider="{headRefModel.teams}"
							addTeam="addTeamHandler(event)" 
							editTeam="editTeamHandler(event)"
							teamSelected="awayTeamSelectedHandler(event)" />
	
	<s:Label x="5"   y="180" id="outLbl" 	 text="{headRefModel.gameModel.outs} OUT  " styleName="statusLabel" />
	<s:HGroup right="5" y="180" >
		<s:Label id="inningLbl"  text="{halfInning} of  " styleName="statusLabel" />	
		<s:Label id="inningLbl2" text="{headRefModel.gameModel.inning}" styleName="statusLabel" />				
	</s:HGroup>
	
	<components:ResultButton x="5"  y="210" id="safeBtn" label="SAFE" 
							 click="safeBtnClickHandler(event)" color="{RefConstants.GREEN}" 
							 skinClass="skinClasses.SafeButtonSkin" />
	<components:ResultButton x="5"  y="322.5" id="outBtn" label="OUT" 
							 click="outBtnClickHandler(event)" color="{RefConstants.RED}" 
							 skinClass="skinClasses.OutButtonSkin"  />

	<components:PitchButton right="5" y="210" id="ballBtn" count="{headRefModel.gameModel.balls}" 
							click="ballBtnClickHandler(event)" skinClass="skinClasses.BallButtonSkin"  />
	<components:PitchButton right="5" y="285" id="strikeBtn" count="{headRefModel.gameModel.strikes}" 
							click="strikeBtnClickHandler(event)" skinClass="skinClasses.StrikeButtonSkin" />
	<components:PitchButton right="5" y="360" id="foulBtn" count="{headRefModel.gameModel.fouls}" 
							click="foulBtnClickHandler(event)" skinClass="skinClasses.FoulButtonSkin" />
	
	<components:UndoButton x="5" bottom="2" 
						   id="undoButton" label="UNDO" 
						   click="undoButtonClickHandler(event)" />

	<components:TimerView right="5" bottom="0"
						  id="gameTimer" gameTimeExpired="gameTimeExpiredHandler(event)" 
						  timeLimit="{headRefModel.gameModel.leagueRules.timeLimitInMilliseconds}"/>
	
</views:AbstractRefView>
